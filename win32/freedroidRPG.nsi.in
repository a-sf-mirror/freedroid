; Copyright (c) 2011 Samuel Degrande
;
; This program is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, either version 3 of the License, or
; (at your option) any later version.
;
; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with this program.  If not, see <http://www.gnu.org/licenses/>.

!define PRODUCT_NAME "FreedroidRPG"
;!define PRODUCT_VERSION "0.15rc1"
!define PRODUCT_PUBLISHER "Freedroid Team"
!define PRODUCT_WEB_SITE "http://www.freedroid.org"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"
;!define SRC_DIR "freedroidrpg-win32"

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "win32\installer-icon.ico"
!define MUI_UNICON "win32\uninstaller-icon.ico"

!define MUI_HEADERIMAGE
!define MUI_HEADERIMAGE_RIGHT
!define MUI_HEADERIMAGE_BITMAP "win32\installer-header.bmp"
!define MUI_HEADERIMAGE_UNBITMAP "win32\installer-header.bmp"

!define MUI_WELCOMEFINISHPAGE_BITMAP   "win32\installer.bmp"
!define MUI_UNWELCOMEFINISHPAGE_BITMAP "win32\installer.bmp"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!insertmacro MUI_PAGE_LICENSE "${SRC_DIR}\COPYING.txt"
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN "$INSTDIR\freedroidRPG.exe"
!define MUI_FINISHPAGE_SHOWREADME "$INSTDIR\README.txt"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_WELCOME
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"
!insertmacro MUI_RESERVEFILE_LANGDLL

; MUI end ------

; Main Install settings
Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "freedroidRPG-${PRODUCT_VERSION}-win32.exe"
InstallDir "$PROGRAMFILES\FreedroidRPG"
InstallDirRegKey HKLM "Software\${PRODUCT_NAME}" "InstallDir"
ShowInstDetails hide
ShowUnInstDetails hide

RequestExecutionLevel admin

Section "FreedroidRPG" Section1

	; Detect previous install
	IfFileExists $INSTDIR\freedroidRPG.exe no_overwrite_error fresh_install

no_overwrite_error:
	MessageBox MB_OK "Previous version of ${PRODUCT_NAME} found$\r$\nUninstall the old version first."
	Abort "Previous version of ${PRODUCT_NAME} found$\r$\nUninstall the old version first."
 
fresh_install:

	; Set Section properties
	SetOverwrite on

	; Set Section Files and Shortcuts
	SetOutPath "$INSTDIR\"

	%INSTALL_FILE_LIST

	File /r "${SRC_DIR}\sound"
	File /r "${SRC_DIR}\map"
	File /r "${SRC_DIR}\graphics"
	File /r "${SRC_DIR}\dialogs"
	File /r "${SRC_DIR}\lua_modules"
	File /r "${SRC_DIR}\locale"

	WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"

	SetShellVarContext all
	CreateShortCut "$DESKTOP\FreedroidRPG.lnk" "$INSTDIR\freedroidRPG.exe"
	CreateDirectory "$SMPROGRAMS\FreedroidRPG"
	CreateShortCut "$SMPROGRAMS\FreedroidRPG\FreedroidRPG.lnk" "$INSTDIR\freedroidRPG.exe"
	CreateShortCut "$SMPROGRAMS\FreedroidRPG\README.lnk" "$INSTDIR\README.txt"
	CreateShortCut "$SMPROGRAMS\FreedroidRPG\Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
	CreateShortCut "$SMPROGRAMS\FreedroidRPG\Uninstall.lnk" "$INSTDIR\uninstall.exe"

SectionEnd

Section -FinishSection

	WriteUninstaller "$INSTDIR\uninstall.exe"

	WriteRegStr HKLM "Software\${PRODUCT_NAME}" "InstallDir" "$INSTDIR"

	WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
	WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninstall.exe"
	WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
	WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
	WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"

SectionEnd

Function un.onUninstSuccess
	HideWindow
	MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd

; Modern install component descriptions
!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
	!insertmacro MUI_DESCRIPTION_TEXT ${Section1} ""
!insertmacro MUI_FUNCTION_DESCRIPTION_END

;Uninstall section
Section Uninstall

	;Remove from registry...
	DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
	DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "Software\${PRODUCT_NAME}"

	; Delete self
	Delete "$INSTDIR\uninstall.exe"

	; Delete Shortcuts
	SetShellVarContext all
	Delete "$DESKTOP\FreedroidRPG.lnk"
	Delete "$SMPROGRAMS\FreedroidRPG\FreedroidRPG.lnk"
	Delete "$SMPROGRAMS\FreedroidRPG\README.lnk"
	Delete "$SMPROGRAMS\FreedroidRPG\Website.lnk"
	Delete "$SMPROGRAMS\FreedroidRPG\Uninstall.lnk"
	RMDir  "$SMPROGRAMS\FreedroidRPG"
	Delete "$INSTDIR\${PRODUCT_NAME}.url"

	; Clean up FreedroidRPG
	%UNINSTALL_FILE_LIST
	
	Delete "$INSTDIR\stderr.txt"
	Delete "$INSTDIR\stdout.txt"

	RMDir /r "$INSTDIR\sound\"
	RMDir /r "$INSTDIR\map\"
	RMDir /r "$INSTDIR\graphics\"
	RMDir /r "$INSTDIR\dialogs\"
	RMDir /r "$INSTDIR\lua_modules\"
	RMDir /r "$INSTDIR\locale\"
	
	RMDir "$INSTDIR\"

	SetAutoClose true

SectionEnd

; eof
